configurations {
    api
    apiClient
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated-sources/api/src/main/java"
        }
        resources {
            srcDir "${buildDir}/generated-sources/api/src/main/resources"
        }
    }

    apiSpecification {
        resources {
            srcDir "${projectDir}/src/main/api-specification"
        }
    }

    apiClient {
        compileClasspath += main.compileClasspath + main.output
        java {
            srcDir "${projectDir}/src/main/client/java"
            srcDir "${buildDir}/generated-sources/api-client/src/main/java"
        }
        resources {
            srcDir "${projectDir}/src/main/client/resources"
            srcDir "${buildDir}/generated-sources/api-client/src/main/resources"
        }
    }
}

dependencies {
    apiClientImplementation sourceSets.main.output

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.29'
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.29'
    implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: '2.29'
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.0'

    api group: 'org.openapitools', name: 'openapi-generator-cli', version: '4.1.1'


}

task generateApi_Code {
    outputs.upToDateWhen { false }
    logging.captureStandardError LogLevel.INFO

    doFirst {
        mkdir("$projectDir/build/generated-sources/api")
    }
    doLast {
        javaexec {
            standardOutput = System.out
            errorOutput = System.err
            main 'org.openapitools.codegen.OpenAPIGenerator'
            classpath += configurations.api
            args = [
                    'generate',
                    '-g', 'spring',
                    '-i', "$projectDir/src/main/api-specification/plant-grow-api.openapi-3.0.yml",
                    '-o', "$projectDir/build/generated-sources/api",
                    '-c', "$projectDir/src/main/api-specification/openapi-config.json",
            ]
        }
    }
}

